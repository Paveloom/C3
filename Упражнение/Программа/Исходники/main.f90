program main ! Программа, демонстрирующая использование модуля
             ! для спектрально-корреляционного анализа временных рядов
use SCATS ! API модуля СКАВР
implicit none

     ! Смотри описание задания и отчет по его
     ! выполнению в файле ../Отчет/Отчет.pdf

     ! Определение экземпляра для использования API модуля СКАВР
     type( SCATS_API ) :: s

     ! Размеры выборок
     integer(4) :: N_1 ! Значение N_1 = N_2 / 2
     integer(4) :: N ! Значение 2 * N / N,
                     ! где 2 * N — длина текущего ряда

     ! Дополнительные переменные
     real(RP) :: arg ! Аргумент синуса и косинуса
     real(RP) :: S_cos, S_sin ! Суммы S_cos и S_sin
     real(RP) :: S_cos_2, S_sin_2 ! Квадраты описанных выше сумм
     real(RP), dimension(0:256) :: S_2 ! Массив значений S_cos_2 + S_sin_2

     ! Вспомогательные переменные
     real(RP) :: pi_delta_t ! Произведение pi * delta_t
     integer(4) :: i, k, k2 ! Счетчики
     real(RP) :: k_d ! Овеществление k

     ! Число pi
     real(RP), parameter :: pi = 4._RP * atan(1._RP)

     ! Примечание: смотри некоторые обозначения
     ! в отчете или в книжке Витязева

     ! Считывание временного ряда из файла
     call s%input%read('Файлы/input')

     ! Примечание: считываемый временной ряд
     ! был сгенерирован с помощью данного модуля,
     ! а его значения намеренно были приведены
     ! к выполнению условия попарного совпадения

     ! Использованные при генерации параметры
     ! указаны в файле gen_params. Некоторые части
     ! данной программы-скрипта опираются
     ! исключительно на эти параметры
     ! или их особенности; например, на размер ряда

     ! Визуализация входных данных
     call s%visualize%input('Файлы/input')

     ! Вычисление периодограммы
     call s%calc_per(use_input=.true.)

     ! Запись результата в файл
     call s%result%write('Файлы/result')

     ! Визуализация периодограммы
     call s%visualize%result('Файлы/result', stage='per')

     ! Примечание: код скрипта для визуализации результата
     ! изменен с целью добавления вертикальной линии
     ! на частоте, равной половине частоты Найквиста

     ! Вычисление произведения pi * delta_t
     pi_delta_t = pi * s%result%delta_t

     ! Определение значения N_1
     N_1 = size(s%result%v) - 1

     ! Определение N
     N = size(s%result%v) / 2 - 1

     ! Вычисление значений суммы S^2_cos + S^2_sin
     ! для вычисленной выше системы частот
     do i = 0, N_1

          ! Обнуление сумм
          S_cos = 0._RP
          S_sin = 0._RP

          do k = 0, N

               ! Вычисление удвоенного значения счетчика
               k2 = k * 2

               ! Овеществление счетчика
               k_d = k

               ! Вычисление аргумента
               arg = pi_delta_t * s%result%v(i) * (4._RP * k_d + 1._RP)

               ! Вычисление сумм
               S_cos = S_cos + s%result%x(k2) * cos(arg)
               S_sin = S_sin + s%result%x(k2) * sin(arg)

          enddo

          ! Вычисление квадратов сумм
          S_cos_2 = S_cos * S_cos
          S_sin_2 = S_sin * S_sin

          ! Вычисление суммы квадратов
          S_2(i) = S_cos_2 + S_sin_2

     enddo

     ! Сохранение значений сумм в массив периодограммы
     s%result%D(:) = S_2(:)

     ! Примечание: данный трюк применяется с той целью,
     ! чтобы отобразить значения сумм на той же системе частот

     ! Запись результата в файл
     call s%result%write('Файлы/S_2')

     ! Визуализация результата
     call s%visualize%result('Файлы/S_2', stage='per', output_file="Фигуры/S_2", title="Периодограмма без множителя", show_sigma=.false.)

     ! Общее освобождение памяти
     call s%deallocate()

end program main